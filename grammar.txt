TERMINALS:

PLUS
MINUS
STAR
SLASH
LPAR
RPAR
SEMICOLON
EQUALS
PERCENT

COMP_EQUALS
COMP_LT
COMP_LTE
COMP_GT
COMP_GTE
COMP_NE

NUMBER
IDENTIFIER
STRING

PRINT
LET
OR
AND
NOT

EOF

---------------------------------------------
GRAMMAR:

script -> statement* EOF

statement -> PRINT expression SEMICOLON | LET IDENTIFIER [EQUALS expression] SEMICOLON | expression SEMICOLON

expression -> IDENTIFIER EQUALS expression | disjunction

disjunction -> conjunction | disjunction OR conjunction

conjunction -> inversion | conjunction AND inversion

inversion -> comparison | NOT inversion

comparison -> arithmatic (compOp arithmatic)*
compOp -> COMP_EQUALS | COMP_LT | COMP_LTE | COMP_GT | COMP_GTE | COMP_NE

arithmatic -> term | arithmatic PLUS term | arithmatic MINUS term

term -> factor | term STAR factor | term SLASH factor | term PERCENT factor

factor -> MINUS* (IDENTIFIER | NUMBER | LPAR expression RPAR | STRING)

---------------------------------------------
GRAMMAR WITHOUT LEFT RECURSION:

script -> statement* EOF

statement -> PRINT expression SEMICOLON | LET IDENTIFIER [EQUALS expression] SEMICOLON | expression SEMICOLON

expression -> IDENTIFIER EQUALS expression | disjunction

disjunction -> conjunction (OR conjunction)*

conjunction -> inversion (AND inversion)*

inversion -> comparison | NOT inversion

comparison -> arithmatic (compOp arithmatic)*
compOp -> COMP_EQUALS | COMP_LT | COMP_LTE | COMP_GT | COMP_GTE | COMP_NE

arithmatic -> term [rightExpression]
rightExpression -> PLUS term [rightExpression] | MINUS term [rightExpression]

term -> factor [rightTerm]
rightTerm -> STAR factor [rightTerm] | SLASH factor [rightTerm] | PERCENT factor [rightTerm]

factor -> MINUS* (IDENTIFIER | NUMBER | LPAR expression RPAR | STRING)